#
# Graffiti build system
# Authors: Rohit Yadav <rohit@scaleninja.com>
#

# Project Info
SET(PROJECT_NAME_SHORT   "Graffiti")
SET(PROJECT_NAME_LONG    "Graffiti")
SET(PROJECT_DESCRIPTION  "Graffiti is an interactive whiteboard for Local Area Networks.")
SET(PROJECT_COPYRIGHT    "Copyright (C) 2009-2015 Rohit Yadav, Abhishek Kumar")
SET(PROJECT_CONTACT      "rohit@scaleninja.com")
SET(PROJECT_VENDOR       "Rohit Yadav")
SET(ORG_WEBSITE          "http://bhaisaab.org")

# Find Git Version Patch
FIND_PROGRAM(GIT git)
IF(GIT)
    EXECUTE_PROCESS(
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        COMMAND ${GIT} rev-parse --short HEAD 
        OUTPUT_VARIABLE GIT_OUT OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    SET(PROJECT_VERSION_PATCH "${GIT_OUT}git")
    MESSAGE(STATUS "Git Version Patch: git_${GIT_OUT}")
ELSE(GIT)
    SET(GIT_OUT 0)
ENDIF(GIT)

# version: 0.2.x
SET(CPACK_PACKAGE_VERSION_MAJOR "0")
SET(CPACK_PACKAGE_VERSION_MINOR "2")
SET(CPACK_PACKAGE_VERSION_PATCH ${PROJECT_VERSION_PATCH})
SET(CPACK_PACKAGE_VERSION ${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH})
SET(PROJECT_VERSION ${CPACK_PACKAGE_VERSION})

STRING(TOLOWER ${PROJECT_NAME_SHORT} PROJECT_NAME_SHORT_LOWER)
PROJECT(${PROJECT_NAME_SHORT_LOWER}-${PROJECT_VERSION})

# Codename of the Project :-)
SET(CODENAME "Khajur")

# System Information
SITE_NAME(HOSTNAME)
SET(SYSNAME ${CMAKE_SYSTEM}-${CMAKE_SYSTEM_PROCESSOR})

# CMake Settings
CMAKE_MINIMUM_REQUIRED(VERSION 2.6.0)
SET(CMAKE_COLOR_MAKEFILE ON)
SET(CMAKE_VERBOSE_MAKEFILE OFF)

# Set path to additional CMake modules
SET(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake ${CMAKE_MODULE_PATH})

# Path to contribs
SET(CONTRIBS_PATH ${CMAKE_CURRENT_SOURCE_DIR}/contribs)

# Configuring build type
IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE "Debug" CACHE STRING
        "Choose the type of build, options are: None Debug Release RelWithDebInfo MinSizeRel."
        FORCE)
    MESSAGE("Setting build type to '${CMAKE_BUILD_TYPE}'")
ELSE(NOT CMAKE_BUILD_TYPE)
    MESSAGE("Build type set to '${CMAKE_BUILD_TYPE}'")
ENDIF(NOT CMAKE_BUILD_TYPE)

IF(WIN32)
    # Building in release for WIN32
    SET(CMAKE_BUILD_TYPE "Release")
    SET_PROPERTY(DIRECTORY APPEND PROPERTY COMPILE_DEFINITIONS_RELEASE QT_NO_DEBUG)
    # Use mingw's sprintf instead of windows's
    ADD_DEFINITIONS( -D__USE_MINGW_ANSI_STDIO=1 )
ENDIF(WIN32)

IF (UNIX)
    SET(WITH_PROFILING FALSE CACHE BOOL "Build with profiling support" )
    SET(CMAKE_INSTALL_PREFIX /usr)
ENDIF(UNIX)

IF(APPLE)
    SET(CMAKE_BUILD_TYPE "Release")
ENDIF(APPLE)

# search for Qt4
IF(WIN32)
    #FindQt4.cmake is so brain-dead when Xcompiling
    SET(QT_LIBRARY_DIR ${CONTRIBS_PATH}/lib)
    SET(QT_INCLUDE_DIR ${CONTRIBS_PATH}/include/qt4)
    ADD_DEFINITIONS( -I${CONTRIBS_PATH}/include/qt4/QtCore)
    ADD_DEFINITIONS( -I${CONTRIBS_PATH}/include/qt4/QtGui)
    ADD_DEFINITIONS( -I${CONTRIBS_PATH}/include/qt4/QtXml)
    ADD_DEFINITIONS( -I${CONTRIBS_PATH}/include/qt4/QtNetwork)
    ADD_DEFINITIONS( -I${CONTRIBS_PATH}/include/qt4/)
    ADD_DEFINITIONS( -DQT_NO_OPENSSL)
    FIND_PACKAGE(Qt4 4.5.1 COMPONENTS QtCore QtXml QtGui QtNetwork REQUIRED )
ELSE(WIN32)
    FIND_PACKAGE(Qt4 4.5.1 COMPONENTS QtCore QtXml QtGui QtSvg QtNetwork REQUIRED )
ENDIF(WIN32)

IF (NOT WIN32)
    #FindQt4.cmake is so brain-dead when Xcompiling
    SET (GRAFFITI_LIBS ${QT_QTCORE_LIBRARY}
          ${QT_QTGUI_LIBRARY}
          ${QT_QTNETWORK_LIBRARY}
          ${QT_QTSVG_LIBRARY}
          ${QT_QTXML_LIBRARY})

    INCLUDE(${QT_USE_FILE})
    ADD_DEFINITIONS(${QT_DEFINITIONS})
ELSE (NOT WIN32)
    SET (GRAFFITI_LIBS
            ${CONTRIBS_PATH}/lib/libQtNetwork.a
            ${CONTRIBS_PATH}/lib/libQtXml.a
            ${CONTRIBS_PATH}/lib/libQtGui.a
            ${CONTRIBS_PATH}/lib/libqtaccessiblewidgets.a
            ${CONTRIBS_PATH}/lib/libQtCore.a
            ws2_32 winmm imm32)
ENDIF(NOT WIN32)

SET (CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

# Copy the sh launcher
IF (NOT WIN32)
    CONFIGURE_FILE("${CMAKE_CURRENT_SOURCE_DIR}/cmake/graffiti.sh" "${CMAKE_CURRENT_BINARY_DIR}/graffiti" COPYONLY)
ENDIF(NOT WIN32)

# common for MAC and UNIX
SET (DEFAULT_BIN_SUBDIR     bin)
SET (DEFAULT_LIB_SUBDIR     lib)
SET (DEFAULT_DATA_SUBDIR    share)
SET (DEFAULT_SRC_SUBDIR     src)
SET (DEFAULT_INCLUDE_SUBDIR include)

SET (GRAFFITI_BIN_SUBDIR     "${DEFAULT_BIN_SUBDIR}"        CACHE STRING "Subdirectory where executables will be installed")
SET (GRAFFITI_LIB_SUBDIR     "${DEFAULT_LIB_SUBDIR}"        CACHE STRING "Subdirectory where libraries will be installed")
SET (GRAFFITI_DATA_SUBDIR    "${DEFAULT_DATA_SUBDIR}"       CACHE STRING "Subdirectory where GRAFFITI data will be installed")
SET (GRAFFITI_SRC_SUBDIR     "${DEFAULT_SRC_SUBDIR}"        CACHE STRING "Subdirectory where GRAFFITI source is")
SET (GRAFFITI_INCLUDE_SUBDIR "${DEFAULT_INCLUDE_SUBDIR}"    CACHE STRING "Subdirectory where header files will be installed")

# full paths for the installation
SET (GRAFFITI_BIN_DIR     ${CMAKE_INSTALL_PREFIX}/${GRAFFITI_BIN_SUBDIR})
SET (GRAFFITI_LIB_DIR     ${CMAKE_INSTALL_PREFIX}/${GRAFFITI_LIB_SUBDIR})
SET (GRAFFITI_INCLUDE_DIR ${CMAKE_INSTALL_PREFIX}/${GRAFFITI_INCLUDE_SUBDIR})
SET (GRAFFITI_DATA_DIR    ${CMAKE_INSTALL_PREFIX}/${GRAFFITI_DATA_SUBDIR}/graffiti/)
SET (GRAFFITI_DOC_DIR     ${CMAKE_INSTALL_PREFIX}/${GRAFFITI_DATA_SUBDIR}/doc/graffiti/)
SET (GRAFFITI_EFFECTS_DIR ${CMAKE_INSTALL_PREFIX}/${GRAFFITI_LIB_SUBDIR}/graffiti/effects/)
SET (GRAFFITI_DESKTOP_DIR ${CMAKE_INSTALL_PREFIX}/${GRAFFITI_DATA_SUBDIR}/applications/)
SET (GRAFFITI_PIXMAPS_DIR ${CMAKE_INSTALL_PREFIX}/${GRAFFITI_DATA_SUBDIR}/pixmaps/)

# manual page - makes sense only on unix systems
IF (UNIX)
    SET (DEFAULT_MANUAL_SUBDIR  man)
    SET (GRAFFITI_MANUAL_SUBDIR  ${DEFAULT_MANUAL_SUBDIR}  CACHE STRING "Subdirectory where manual files will be installed")
    MARK_AS_ADVANCED (GRAFFITI_MANUAL_SUBDIR)
    SET (GRAFFITI_MANUAL_DIR  ${CMAKE_INSTALL_PREFIX}/${GRAFFITI_MANUAL_SUBDIR})
ENDIF (UNIX)

#SUBDIRS(ts)
SUBDIRS(src)
SUBDIRS(nsis)

# Copy stuff to doc subdirectory
INSTALL (FILES AUTHORS COPYING INSTALL NEWS README THANKS
         DESTINATION ${GRAFFITI_DOC_DIR})

# Copy .desktop file
INSTALL (FILES share/graffiti.desktop DESTINATION ${GRAFFITI_DESKTOP_DIR})

# Copy the GRAFFITI pixmap
INSTALL (FILES share/graffiti.png DESTINATION ${GRAFFITI_PIXMAPS_DIR})

# Copy the share directory
INSTALL (DIRECTORY share/ DESTINATION ${GRAFFITI_DATA_DIR}
         PATTERN "*.desktop" EXCLUDE
         PATTERN "*.ico" EXCLUDE
         PATTERN "*.icns" EXCLUDE
         PATTERN "graffiti.png" EXCLUDE)

#############################################################
# Package Settings

INCLUDE(InstallRequiredSystemLibraries)

SET(CPACK_SYSTEM_NAME ${CMAKE_SYSTEM_NAME})
SET(CPACK_PACKAGE_NAME ${PROJECT_NAME_SHORT})
SET(CPACK_PACKAGE_DESCRIPTION ${PROJECT_NAME_LONG})
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${PROJECT_NAME_SHORT} - ${PROJECT_DESCRIPTION}")
SET(CPACK_PACKAGE_VENDOR ${PROJECT_VENDOR})
SET(CPACK_PACKAGE_CONTACT ${PROJECT_CONTACT})
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_SOURCE_DIR}/README")
SET(CPACK_RESOURCE_FILE_README ${CMAKE_SOURCE_DIR}/README)
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/COPYING")
SET(CPACK_PACKAGE_INSTALL_DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}")

# Force Package Name
SET(CPACK_PACKAGE_FILE_NAME ${PROJECT_NAME}-${CPACK_SYSTEM_NAME}-${CMAKE_SYSTEM_PROCESSOR})

# NSIS Windows Installer
IF(WIN32 AND NOT UNIX)
    # There is a bug in NSI that does not handle full unix paths properly. Make
    # sure there is at least one set of four (4) backslashes.
    SET(CPACK_PACKAGE_ICON "${CMAKE_SOURCE_DIR}/share\\\\graffiti.png")
    SET(CPACK_NSIS_INSTALLED_ICON_NAME "\\\\graffiti.exe")
    SET(CPACK_NSIS_DISPLAY_NAME "${CPACK_PACKAGE_INSTALL_DIRECTORY} graffiti")
    SET(CPACK_NSIS_HELP_LINK "http:\\\\\\\\bhaisaab.org")
    SET(CPACK_NSIS_URL_INFO_ABOUT "http:\\\\\\\\bhaisaab.org")
    SET(CPACK_NSIS_CONTACT ${PROJECT_CONTACT})
    SET(CPACK_NSIS_MODIFY_PATH ON)
ELSE(WIN32 AND NOT UNIX)
    #SET(CPACK_STRIP_FILES "GRAFFITI")
    #SET(CPACK_SOURCE_STRIP_FILES "")
ENDIF(WIN32 AND NOT UNIX)

# RPM packages
INCLUDE ( ${CMAKE_MODULE_PATH}/RpmBuild.cmake )
IF ( RPMBUILD_FOUND AND NOT WIN32 )
    SET(CPACK_GENERATOR "RPM")
    SET(CPACK_SET_DESTDIR TRUE)
    #SET(CPACK_RPM_PACKAGE_REQUIRES ${PACKAGE_REQUIRES})
ENDIF ( RPMBUILD_FOUND AND NOT WIN32 )

# Debian packages
INCLUDE ( ${CMAKE_MODULE_PATH}/DpkgBuild.cmake )
IF ( DPKG_FOUND AND NOT WIN32 )
    SET(CPACK_GENERATOR "DEB")
    SET(CPACK_SET_DESTDIR TRUE)
    SET(CPACK_DEBIAN_PACKAGE_DEPENDS "libqt4-gui ( >= 4.6 ), libqt4-network ( >= 4.6 ), libqt4-svg ( >= 4.6 ), libqt4-xml ( >= 4.6 )")
ENDIF ( DPKG_FOUND AND NOT WIN32 )

# Mac Bundle
IF( APPLE )
    SET(CPACK_GENERATOR "DragNDrop")

    #Libraries are bundled directly
    SET(CPACK_COMPONENT_LIBRARIES_HIDDEN TRUE)

    # Bundle Properties
    SET(MACOSX_BUNDLE_BUNDLE_NAME graffiti)
    SET(MACOSX_BUNDLE_BUNDLE_VERSION ${PROJECT_VERSION})
    SET(MACOSX_BUNDLE_SHORT_VERSION_STRING ${PROJECT_VERSION})
    SET(MACOSX_BUNDLE_LONG_VERSION_STRING "Version ${PROJECT_VERSION}")

    #SET(CPACK_STRIP_FILES TRUE)
ENDIF( APPLE )

SET(CPACK_PACKAGE_EXECUTABLES "graffiti" "GRAFFITI")
INCLUDE(CPack)
